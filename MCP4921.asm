;------------------------------------------------------------------------------
; Подпрограммы для работы с MCP4921
; 12-битный ЦАП
; SPI интерфейс
; с возможностью изменения коэффициента передачи выходного буфера
; вход для внешнего источника опорного напряжения VREF
;
; Формат посылаемой команды:
; bit 15   = 0 - запись в регистр ЦАП
;          = 1 - игнорировать эту команду
; bit 14   = 1 - буферизованный вход Vref
;          = 0 - небуферизованный вход Vref
; bit 13   = 1 - 1x(Vout = Vref*D/4096)
;          = 0 - 2x(Vout = 2*Vref*D/4096)
; bit 12   = 1 - активный режим
;          = 0 - устройство выключено. Vout присоединен к резистору 500 кОм
; bit 11-0  D11:D0  биты данных ЦАП
; 
; 
; Перед использованием необходимо проинициализировать SPI
; 
; Задать DAC_CS_PORT, DAC_CS_DDR, DAC_CS
;------------------------------------------------------------------------------
#ifndef _MCP4921_ASM_
#define _MCP4921_ASM_

#define DAC_A 0x00
#define DAC_B 0x80

#define Buffer_on 0x40
#define Buffer_off 0x00

#define Gain_1X 0x20
#define Gain_2X 0x00

#define Active 0x10
#define Shutdown 0x00

.ifndef __zero_reg__
.def __zero_reg__ = r2
.endif

;------------------------------------------------------------------------------
; Инициализация ЦАП MCP4921
; 
; Вызовы: DAC_SET
; Используются: r16*, r17*
; Вход: -
; Выход: -
;------------------------------------------------------------------------------
DAC_INIT:
			; настройка линий ввода/вывода
			sbi		DAC_CS_DDR,DAC_CS	; DAC CS output
			sbi		DAC_CS_PORT,DAC_CS	; CS=1
			; Устанавливаем начальное значение ЦАП
			sts		DAC+0,__zero_reg__
			sts		DAC+1,__zero_reg__
			rcall	DAC_SET
			ret


;------------------------------------------------------------------------------
; Установка значения ЦАП MCP4921
; 
; Вызовы: SPI_RW
; Используются: r16*
; Вход: DAC (2 bytes)
; Выход: -
;------------------------------------------------------------------------------
DAC_SET:
			cbi		DAC_CS_PORT,DAC_CS	; CS=0
			lds		r16,DAC+1			; старший байт
			ori		r16,(DAC_A|Buffer_on|Gain_1X|Active)	; буферизованный вход Vref + GAIN 1x
			rcall	SPI_RW
			lds		r16,DAC+0			; младший байт
			rcall	SPI_RW
			sbi		DAC_CS_PORT,DAC_CS	; CS=1
			ret

#endif  /* _MCP4921_ASM_ */

;------------------------------------------------------------------------------
; End of file
;------------------------------------------------------------------------------
